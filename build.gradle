import java.text.SimpleDateFormat

plugins {
    id 'java'
    id 'groovy'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.8.5'
    id 'com.github.rodm.teamcity-server' version '1.2.2' // see https://github.com/rodm/gradle-teamcity-plugin
    id 'com.github.rodm.teamcity-environments' version '1.2.2'
}

ext {
    CI = System.getenv().containsKey('CI') || project.properties.containsKey('CI')
    RELEASE = project.properties.containsKey('RELEASE')
    baseVersion = '1.0'
    buildTime = new Date()
}

def buildTimestamp = CI ? timestamp(buildTime) : 'prerelease'
def snapshotVersion = RELEASE ? '' : buildTimestamp

group = 'nu.studer'
version = baseVersion + (snapshotVersion ? "-$snapshotVersion" : '')

dependencies {
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'com.google.guava:guava:29.0-jre'

    agent project(path: ':agent', configuration: 'plugin')

    testImplementation("org.spockframework:spock-core:1.3-groovy-2.5") {
        exclude module: 'groovy-all'
    }
    testRuntimeOnly 'org.junit.vintage:junit-vintage-engine:5.6.2'
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

test {
    useJUnitPlatform() {
        includeEngines 'junit-vintage'
    }
}

teamcity {
    version = '2019.1' // TeamCity API version

    server {
        descriptor {
            name = 'GradleBuildScanIntegration'
            displayName = 'Integration for Gradle and Maven build scans'
            description = 'Provides easy navigation from TeamCity builds to Gradle and Maven build scans'
            version = project.version
            vendorName = 'Etienne Studer'
            vendorUrl = 'https://github.com/etiennestuder'

            // optional properties
            email = 'etienne@studer.nu'
            downloadUrl = 'https://github.com/etiennestuder/teamcity-build-scan-plugin'
            useSeparateClassloader = true
        }
    }

    environments {
        downloadsDir = mkdir('.teamcity/dists')
        baseHomeDir = mkdir('.teamcity/servers')
        baseDataDir = mkdir('.teamcity/data')

        teamcity2019_1 {
            version = "2019.1.3"
            serverOptions '-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=5005'
            agentOptions '-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=5006'
        }
    }
}

task sources(type: Jar) {
    from sourceSets.main.allJava
}

publishing.publications {
    PluginPublication(MavenPublication) {
        artifact serverPlugin
        artifact sources {
            classifier 'sources'
        }
    }
}

bintray {
    user = findProperty('bintrayUser')
    key = findProperty('bintrayKey')
    publications = ['PluginPublication']

    dryRun = true
    publish = true
    override = false

    pkg {
        repo = 'teamcity-plugins'
        name = 'teamcity-build-scan-plugin'

        version {
            name = project.version
            released = project.buildTime
            vcsTag = project.version
        }
    }
}

tasks.build.dependsOn sources

static timestamp(Date date) {
    def timestampFormat = new SimpleDateFormat('yyyyMMddHHmmss')
    timestampFormat.timeZone = TimeZone.getTimeZone('UTC')
    timestampFormat.format(date)
}
