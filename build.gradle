import java.text.SimpleDateFormat

plugins {
    id 'java'
    id 'groovy'
    id 'io.github.rodm.teamcity-server' version '1.5.5' // see https://github.com/rodm/gradle-teamcity-plugin
    id 'io.github.rodm.teamcity-environments' version '1.5.5'
    id 'com.github.breadmoirai.github-release' version '2.5.2'
    id 'org.nosphere.gradle.github.actions' version '1.4.0'
}

ext {
    CI = System.getenv().containsKey('CI') || project.properties.containsKey('CI')
    RELEASE = project.properties.containsKey('RELEASE')
    baseVersion = '0.36'
    buildTime = new Date()
}

def buildTimestamp = CI ? timestamp(buildTime) : 'prerelease'
def snapshotVersion = RELEASE ? '' : buildTimestamp

group = 'nu.studer'
version = baseVersion + (snapshotVersion ? "-$snapshotVersion" : '')

def teamCityApiVersion = '2020.1'

dependencies {
    implementation 'com.google.code.gson:gson:2.12.1'
    implementation 'com.google.guava:guava:33.4.6-jre'

    provided "org.jetbrains.teamcity:oauth:$teamCityApiVersion"

    agent project(path: ':agent', configuration: 'plugin')

    testImplementation 'org.spockframework:spock-core:2.3-groovy-4.0'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
        vendor = JvmVendorSpec.BELLSOFT
    }
}

test {
    useJUnitPlatform()
}

teamcity {
    version = teamCityApiVersion

    server {
        descriptor {
            name = 'GradleBuildScanIntegration'
            displayName = 'Integration for Gradle and Maven build scans'
            description = 'Provides easy navigation from TeamCity builds to Gradle and Maven build scans'
            version = project.version
            vendorName = 'Etienne Studer'
            vendorUrl = 'https://github.com/etiennestuder'

            // optional properties
            email = 'etienne@studer.nu'
            downloadUrl = 'https://github.com/etiennestuder/teamcity-build-scan-plugin'
            useSeparateClassloader = true
        }

        publish {
            channels = ['Stable']
            token = System.getenv('TEAMCITY_PLUGIN_PUBLISH_TOKEN')
            notes = layout.projectDirectory.file('release/changes.md').asFile.text.trim()
        }
    }

    environments {
        downloadsDir = mkdir('.teamcity/dists')
        baseHomeDir = mkdir('.teamcity/servers')
        baseDataDir = mkdir('.teamcity/data')

        teamcity2022_4 {
            version = '2022.04'
            serverOptions '-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=5005'
            agentOptions '-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=5006'
        }
    }
}

githubRelease {
    token = System.getenv('TEAMCITY_PLUGIN_GIT_TOKEN') ?: ''
    owner = 'etiennestuder'
    repo = 'teamcity-build-scan-plugin'
    targetCommitish = 'main'
    releaseName = gitHubReleaseName()
    tagName = gitReleaseTag()
    prerelease = !RELEASE
    overwrite = !RELEASE
    generateReleaseNotes = false
    body = layout.projectDirectory.file('release/changes.md').asFile.text.trim()
    releaseAssets(tasks.named('serverPlugin'))
}

repositories {
    mavenCentral()
}

tasks.register('createReleaseTag', CreateGitTag) {
    tagName = gitReleaseTag()
    overwriteExisting = !RELEASE
}

tasks.named('githubRelease') {
    dependsOn('createReleaseTag')
}

def gitHubReleaseName() {
    return RELEASE ? version.toString() : 'Development Build'
}

def gitReleaseTag() {
    return RELEASE ? "v${version}" : 'development-latest'
}

static timestamp(Date date) {
    def timestampFormat = new SimpleDateFormat('yyyyMMddHHmmss')
    timestampFormat.timeZone = TimeZone.getTimeZone('UTC')
    timestampFormat.format(date)
}
