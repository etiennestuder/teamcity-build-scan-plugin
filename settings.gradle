plugins {
    id 'com.gradle.develocity' version '3.17.3'
    id 'com.gradle.common-custom-user-data-gradle-plugin' version '2.0.1'
    id 'org.gradle.toolchains.foojay-resolver-convention' version '0.8.0'
}

def isCI = System.getenv('CI') != null

develocity {
    server = 'https://etiennestuder.gradle-enterprise.cloud'

    buildScan {
        publishing.onlyIf { it.isAuthenticated() }
        uploadInBackground = !isCI
        obfuscation { ipAddresses { addresses -> addresses.collect { address -> '0.0.0.0' } } }
    }
}

buildCache {
    local {
        enabled = true
    }

    remote(develocity.buildCache) {
        enabled = true
        // Check access key presence to avoid build cache errors on PR builds when access key is not present
        def accessKey = System.getenv("GRADLE_ENTERPRISE_ACCESS_KEY")
        push = isCI && accessKey
    }
}

include ':agent'
include ':agent:service-message-maven-extension'

rootProject.name = 'teamcity-build-scan-plugin'
