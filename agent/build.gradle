plugins {
    id 'groovy'
    id 'io.github.rodm.teamcity-agent'
}

repositories {
    mavenCentral()
}

configurations {
    mvnExtensions {
        canBeConsumed = false
        canBeResolved = true
    }
}

dependencies {
    mvnExtensions project(path: ':agent:service-message-maven-extension', configuration: 'mvnExtension')
    mvnExtensions 'com.gradle:develocity-maven-extension:1.23.2'
    mvnExtensions 'com.gradle:common-custom-user-data-maven-extension:2.0.1'

    testImplementation gradleTestKit()
    testImplementation ('io.ratpack:ratpack-groovy-test:2.0.0-rc-1') {
        exclude group: 'org.codehaus.groovy', module: 'groovy-all'
    }
    testImplementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-smile:2.18.3'
    testImplementation('org.spockframework:spock-core:2.3-groovy-4.0')
    testImplementation('org.spockframework:spock-junit4:2.3-groovy-4.0')
}

def generateInitScript = tasks.register('generateInitScript') {
    def referenceInitScript = file('src/main/resources/init-scripts/develocity-injection.init.gradle')
    def buildScanCollector = file('src/main/resources/init-scripts/build-scan-collector.groovy')
    def generatedInitScript = project.layout.buildDirectory.file('generated-resources/build-scan-init.gradle')
    inputs.file(referenceInitScript)
    inputs.file(buildScanCollector)
    outputs.file(generatedInitScript)
    doLast {
        generatedInitScript.get().asFile.text =
            referenceInitScript.text.replace("class BuildScanCollector {}", buildScanCollector.text)
    }
}

processResources {
    from configurations.mvnExtensions
    from generateInitScript
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
        vendor = JvmVendorSpec.BELLSOFT
    }
}

test {
    useJUnitPlatform()
    maxParallelForks = Runtime.runtime.availableProcessors().intdiv(2) ?: 1
}

tasks.withType(JavaCompile).configureEach {
    javaCompiler = javaToolchains.compilerFor {
        String javaCompilerVersion = findProperty('javaCompilerVersion') ?: '8'
        languageVersion = JavaLanguageVersion.of(javaCompilerVersion)
    }
}

teamcity {
    agent {
        archiveName = 'teamcity-build-scan-plugin-agent.zip'
        descriptor {
            pluginDeployment {
                useSeparateClassloader = true
            }
        }
    }
}
