import org.gradle.util.GradleVersion

if (GradleVersion.current() < GradleVersion.version('4.1')) {
    logger.warn("TeamCity Build Scan plugin requires at least Gradle 4.1. Build uses Gradle ${GradleVersion.current()}.")
    return
}

initscript {
    def gePluginVersion = System.getProperty('teamCityBuildScanPlugin.gradle.enterprise.plugin.version')
    def ccudPluginVersion = System.getProperty('teamCityBuildScanPlugin.ccud.plugin.version')
    def atLeastGradle5 = GradleVersion.current() >= GradleVersion.version('5.0')

    if (gePluginVersion && !gradle.getParent()) {
        repositories {
            maven { url 'https://plugins.gradle.org/m2' }
        }
        dependencies {
            classpath atLeastGradle5 ?
                    "com.gradle:gradle-enterprise-gradle-plugin:$gePluginVersion" :
                    "com.gradle:build-scan-plugin:$gePluginVersion"

            if (atLeastGradle5 && ccudPluginVersion) {
                classpath "com.gradle:common-custom-user-data-gradle-plugin:$ccudPluginVersion"
            }
        }
    }
}

logger.quiet(generateBuildScanLifeCycleMessage('BUILD_STARTED'))

def BUILD_SCAN_PLUGIN_ID = 'com.gradle.build-scan'
def BUILD_SCAN_PLUGIN_CLASS = 'com.gradle.scan.plugin.BuildScanPlugin'

def CCUD_PLUGIN_ID = 'com.gradle.common-custom-user-data-gradle-plugin'
def CCUD_PLUGIN_CLASS = 'com.gradle.CommonCustomUserDataGradlePlugin'

def GRADLE_ENTERPRISE_PLUGIN_ID = 'com.gradle.enterprise'
def GRADLE_ENTERPRISE_PLUGIN_CLASS = 'com.gradle.enterprise.gradleplugin.GradleEnterprisePlugin'
def GRADLE_ENTERPRISE_EXTENSION_CLASS = 'com.gradle.enterprise.gradleplugin.GradleEnterpriseExtension'

def geUrl = System.getProperty('teamCityBuildScanPlugin.gradle.enterprise.url')
def autoApplyGePlugin = System.getProperty('teamCityBuildScanPlugin.gradle.enterprise.plugin.version') && !gradle.getParent()
def autoApplyCcudPlugin = System.getProperty('teamCityBuildScanPlugin.ccud.plugin.version') && GradleVersion.current() >= GradleVersion.version('5.0')

def buildScanPublishedAction = { def buildScan ->
    if (buildScan.metaClass.respondsTo(buildScan, 'buildScanPublished', Action)) {
        buildScan.buildScanPublished { scan ->
            logger.quiet(generateBuildScanLifeCycleMessage("BUILD_SCAN_URL:${scan.buildScanUri.toString()}"))
        }
    }
}

if (GradleVersion.current() < GradleVersion.version('6.0')) {
    // Gradle 4.1+ and 5.+
    rootProject {
        if (autoApplyGePlugin && !pluginManager.hasPlugin(BUILD_SCAN_PLUGIN_ID)) {
            pluginManager.apply(initscript.classLoader.loadClass(BUILD_SCAN_PLUGIN_CLASS))
        }

        if (autoApplyCcudPlugin && !pluginManager.hasPlugin(CCUD_PLUGIN_ID)) {
            pluginManager.apply(initscript.classLoader.loadClass(CCUD_PLUGIN_CLASS))
        }

        pluginManager.withPlugin(BUILD_SCAN_PLUGIN_ID) {
            buildScanPublishedAction(buildScan)
            buildScan {
                afterEvaluate {
                    server = geUrl ?: server
                    publishAlwaysIf(autoApplyGePlugin)
                }
            }
        }
    }
} else {
    // Gradle 6.+
    gradle.settingsEvaluated { settings ->
        if (autoApplyGePlugin && !settings.pluginManager.hasPlugin(GRADLE_ENTERPRISE_PLUGIN_ID)) {
            settings.pluginManager.apply(initscript.classLoader.loadClass(GRADLE_ENTERPRISE_PLUGIN_CLASS))
        }

        if (autoApplyCcudPlugin && !settings.pluginManager.hasPlugin(CCUD_PLUGIN_ID)) {
            settings.pluginManager.apply(initscript.classLoader.loadClass(CCUD_PLUGIN_CLASS))
        }

        extensionsWithPublicType(settings, GRADLE_ENTERPRISE_EXTENSION_CLASS).each { ext ->
            buildScanPublishedAction(ext.buildScan)
            ext.server = geUrl ?: ext.server
            ext.buildScan {
                publishAlwaysIf(autoApplyGePlugin)
            }
        }
    }
}

static def extensionsWithPublicType(def container, String publicType) {
    container.extensions.extensionsSchema.elements
            .findAll { it.publicType.concreteClass.name == publicType }
            .collect { container[it.name] }
}

static String generateBuildScanLifeCycleMessage(def attribute) {
    return "##teamcity[nu.studer.teamcity.buildscan.buildScanLifeCycle '${escape(attribute as String)}']" as String
}

static String escape(String value) {
    return value?.toCharArray()?.collect{ch -> escapeChar(ch) }?.join()
}

static String escapeChar(char ch) {
    String escapeCharacter = "|"
    switch (ch) {
        case '\n': return escapeCharacter + "n";
        case '\r': return escapeCharacter + "r";
        case '|': return escapeCharacter + "|";
        case '\'': return escapeCharacter + "\'";
        case '[': return escapeCharacter + "[";
        case ']': return escapeCharacter + "]";
        default: return ch < 128 ? ch as String : escapeCharacter + String.format("0x%04x", (int)ch);
    }
}